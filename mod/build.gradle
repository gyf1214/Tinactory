archivesBaseName = "tinactory"
apply plugin: 'jacoco'

minecraft {
    runs {
        client {
            workingDirectory rootProject.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'

            ideaModule rootProject.name + "." + project.name + ".main"

            mods {
                tinactory {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory rootProject.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'

            ideaModule rootProject.name + "." + project.name + ".main"

            mods {
                tinactory {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory rootProject.file('run/gameTestServer')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'
            args '--mixin.config', 'mixins.tinactory.test.json'

            ideaModule rootProject.name + "." + project.name + ".test"

            mods {
                tinactory {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
    }
}

dependencies {
    // mixin
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    compileOnly "org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}:api"
    runtimeOnly fg.deobf("org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}")

    // JEI
    implementation fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    // CTM
    runtimeOnly fg.deobf("curse.maven:ctm-267602:${ctm_id}")

    // waila
    implementation fg.deobf("curse.maven:jade-324717:${jade_id}")

    testImplementation platform('org.junit:junit-bom:5.12.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testCompileOnly "org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}:api"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

mixin {
    add sourceSets.main, 'mixins.tinactory.refmap.json'
    add sourceSets.test, 'mixins.tinactory.test.refmap.json'
    config 'mixins.tinactory.jei.json'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    exclude '.cache/'
}

configurations {
    api
    dev
}

task apiJar(type: Jar) {
    archiveClassifier = 'api'
    include "org/shsts/tinactory/api/**"
    from sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task devJar(type: Jar) {
    archiveClassifier = 'dev'
    from sourceSets.main.output
}

artifacts {
    api apiJar
    dev devJar
}

build.dependsOn apiJar, sourcesJar

test {
    useJUnitPlatform()
    workingDir rootProject.file('run/unitTest')
    doFirst {
        workingDir.mkdirs()
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    classDirectories.setFrom(files(
        fileTree(dir: "$buildDir/classes/java/main", include: ['org/shsts/tinactory/core/**']),
        fileTree(dir: "$buildDir/classes/kotlin/main", include: ['org/shsts/tinactory/core/**'])
    ))
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact sourcesJar
        }
    }
}
